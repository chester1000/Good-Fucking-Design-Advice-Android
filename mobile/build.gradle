apply plugin: 'com.android.application'

def String safeGet(String name, String defaultValue = '') {
    return hasProperty(name) ? project[name] : defaultValue
}

android {
    compileSdkVersion 22
    buildToolsVersion "22"

    defaultConfig {
        applicationId "com.meeDamian.designAdvice"
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 14
        versionName "1.6"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {
            storeFile file(safeGet('STORE_FILE'))
            storePassword safeGet('STORE_PASSWORD')
            keyAlias safeGet('KEY_ALIAS')
            keyPassword safeGet('KEY_PASSWORD')
        }
    }

    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release as Object
            minifyEnabled true
            zipAlignEnabled true

            android.applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    if (output.name == "release") {
                        String fileName = [
                            defaultConfig.applicationId,
                            project.name,
                            defaultConfig.versionName,
                            defaultConfig.versionCode
                         ].join('-') + '.apk'

                        File dir = new File(
                            safeGet('RELEASES_PARENT_DIR', '~/APKs'),
                            safeGet('FOLDER_NAME', project.group as String)
                        )

                        if (!dir.exists())
                            dir.mkdirs()

                        output.outputFile = new File(dir, fileName)
                    }
                }
            }
        }
    }
}

dependencies {
    //    provided 'com.squareup.dagger:dagger-compiler:1.2.1'
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':common')
    wearApp project(':wear')
    compile 'com.google.android.gms:play-services-cast:6.5.87'
    compile 'com.google.android.gms:play-services-base:6.5.87'
    compile 'com.google.android.gms:play-services-appindexing:6.5.87'
    compile 'com.android.support:appcompat-v7:22.0.0'
    compile 'com.android.support:support-v4:22.0.0'
    compile 'com.android.support:support-annotations:21.0.3'
    compile 'uk.co.chrisjenx:calligraphy:2.0.0'
    compile project(':..:AutoResizeTextView:libAutoResizeTextView')
}
