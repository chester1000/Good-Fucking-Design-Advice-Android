apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "22"

    defaultConfig {
        applicationId "com.meeDamian.designAdvice"
        minSdkVersion 16
        targetSdkVersion 22
        versionCode 3
        versionName "1.1"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {
            storeFile file(project.STORE_FILE)
            storePassword project.STORE_PASSWORD as String
            keyAlias project.KEY_ALIAS as String
            keyPassword project.KEY_PASSWORD as String
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release as Object
            minifyEnabled true
            zipAlignEnabled true

            android.applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    if (output.name == "release") {
                        def file = output.outputFile
                        String parent = file.parent

                        if (project.hasProperty('OUTPUT_DIR') && new File(project.OUTPUT_DIR as String).exists())
                            parent = project.OUTPUT_DIR

                        output.outputFile = new File(
                            parent,
                            file.name.replace(
                                ".apk",
                                "-" + (defaultConfig.versionName as String) + "-build" + (defaultConfig.versionCode as String) + ".apk"
                            ) as String
                        )
                    }
                }
            }
        }
    }
}

dependencies {
//    provided 'com.squareup.dagger:dagger-compiler:1.2.1'

    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':common')
    wearApp project(':wear')
    compile 'com.google.android.gms:play-services:6.5.87'
    compile 'com.android.support:appcompat-v7:22.0.0'
    compile 'com.android.support:support-v4:22.0.0'
    compile 'com.android.support:support-annotations:21.0.3'
    compile 'uk.co.chrisjenx:calligraphy:2.0.0'
}
